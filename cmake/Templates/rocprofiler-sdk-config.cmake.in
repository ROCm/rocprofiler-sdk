# - Config file for @PROJECT_NAME@ and its component libraries
# It defines the following variables:
#
# @PROJECT_NAME@-sdk_VERSION
# @PROJECT_NAME@-sdk_INCLUDE_DIR
# @PROJECT_NAME@-sdk_LIB_DIR
# @PROJECT_NAME@-sdk_LIBRARIES
#
# It provides the following interface libraries:
#
# @PROJECT_NAME@::@PROJECT_NAME@
# @PROJECT_NAME@-sdk::@PROJECT_NAME@-sdk
#

# compute paths
get_filename_component(@PROJECT_NAME@-sdk_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

# version
set(@PROJECT_NAME@-sdk_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

set_and_check(@PROJECT_NAME@-sdk_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(@PROJECT_NAME@-sdk_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")
get_filename_component(@PROJECT_NAME@-sdk_ROOT_DIR ${@PROJECT_NAME@-sdk_INCLUDE_DIR} PATH)

# extra validation
set_and_check(@PROJECT_NAME@_INCLUDE_ROCPROFILER_SDK_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@/@PROJECT_NAME@-sdk")
set_and_check(@PROJECT_NAME@_LIB_ROCPROFILER_SDK_DIR "@PACKAGE_LIB_INSTALL_DIR@/@PROJECT_NAME@-sdk")

set(@PROJECT_NAME@-sdk_LIBRARIES)

# add interface library
add_library(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE IMPORTED)
add_library(@PROJECT_NAME@-sdk::@PROJECT_NAME@-sdk ALIAS @PROJECT_NAME@::@PROJECT_NAME@)

if(@PROJECT_NAME@_BUILD_TREE AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-sdk-build-config.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-sdk-build-config.cmake")
else()
    find_package(
        AMDDeviceLibs
        REQUIRED
        CONFIG
        HINTS
        @AMDDeviceLibs_DIR@
        @rocm_version_DIR@
        PATHS
        @AMDDeviceLibs_DIR@
        @rocm_version_DIR@)

    find_package(
        amd_comgr
        @amd_comgr_VERSION@
        EXACT
        REQUIRED
        CONFIG
        HINTS
        @amd_comgr_DIR@
        @rocm_version_DIR@
        PATHS
        @amd_comgr_DIR@
        @rocm_version_DIR@)

    find_package(
        hsa-runtime64
        @hsa-runtime64_VERSION@
        EXACT
        REQUIRED
        CONFIG
        HINTS
        @hsa-runtime64_DIR@
        @rocm_version_DIR@
        PATHS
        @hsa-runtime64_DIR@
        @rocm_version_DIR@)

    find_package(
        hip
        @hip_VERSION@
        EXACT
        REQUIRED
        CONFIG
        HINTS
        @hip_DIR@
        @rocm_version_DIR@
        PATHS
        @hip_DIR@
        @rocm_version_DIR@)

    include("${@PROJECT_NAME@-sdk_CMAKE_DIR}/@PROJECT_NAME@-sdk-library-targets.cmake")

    # Library dependencies
    foreach(TARG @PROJECT_BUILD_TARGETS@)
        set(TARG @PROJECT_NAME@::@PROJECT_NAME@-${TARG})
        if(NOT @PROJECT_NAME@_FIND_COMPONENTS)
            list(APPEND @PROJECT_NAME@-sdk_LIBRARIES ${TARG})
            target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@
                                  INTERFACE ${TARG})
        endif()
    endforeach()

    if(@PROJECT_NAME@_FIND_COMPONENTS)
        foreach(COMP ${@PROJECT_NAME@_FIND_COMPONENTS})
            set(TARG @PROJECT_NAME@::${COMP})
            if(TARGET ${TARG})
                set(@PROJECT_NAME@-sdk_${COMP}_FOUND 1)
                list(APPEND @PROJECT_NAME@-sdk_LIBRARIES ${TARG})
                target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE ${TARG})
            else()
                set(@PROJECT_NAME@-sdk_${COMP}_FOUND 0)
            endif()
        endforeach()
    endif()
endif()

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    @PROJECT_NAME@-sdk
    FOUND_VAR @PROJECT_NAME@-sdk_FOUND
    VERSION_VAR @PROJECT_NAME@-sdk_VERSION
    REQUIRED_VARS @PROJECT_NAME@-sdk_ROOT_DIR @PROJECT_NAME@-sdk_INCLUDE_DIR @PROJECT_NAME@-sdk_LIB_DIR
                  @PROJECT_NAME@-sdk_LIBRARIES @PROJECT_NAME@-sdk_VERSION
    HANDLE_COMPONENTS HANDLE_VERSION_RANGE)
