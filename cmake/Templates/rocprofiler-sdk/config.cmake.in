# - Config file for @PACKAGE_NAME@ and its component libraries
# It defines the following variables:
#
# @PACKAGE_NAME@_VERSION
# @PACKAGE_NAME@_INCLUDE_DIR
# @PACKAGE_NAME@_LIB_DIR
# @PACKAGE_NAME@_LIBRARIES
#
# It provides the following interface libraries:
#
# @PROJECT_NAME@::@PROJECT_NAME@
# @PACKAGE_NAME@::@PACKAGE_NAME@
#

# compute paths
get_filename_component(@PACKAGE_NAME@_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

# version
set(@PACKAGE_NAME@_VERSION @PROJECT_VERSION@)

@PACKAGE_INIT@

set_and_check(@PACKAGE_NAME@_INCLUDE_DIR "@PACKAGE_INCLUDE_INSTALL_DIR@")
set_and_check(@PACKAGE_NAME@_LIB_DIR "@PACKAGE_LIB_INSTALL_DIR@")
get_filename_component(@PACKAGE_NAME@_ROOT_DIR ${@PACKAGE_NAME@_INCLUDE_DIR} PATH)

# extra validation
foreach(_@PACKAGE_NAME@_SUBDIR @PROJECT_EXTRA_DIRS@)
    set_and_check(_@PACKAGE_NAME@_SUBDIR_CHECK
                  "${PACKAGE_PREFIX_DIR}/${_@PACKAGE_NAME@_SUBDIR}")
    unset(_@PACKAGE_NAME@_SUBDIR_CHECK)
endforeach()

set(@PACKAGE_NAME@_LIBRARIES)

# add interface library
add_library(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE IMPORTED)
add_library(@PACKAGE_NAME@::@PACKAGE_NAME@ ALIAS @PROJECT_NAME@::@PROJECT_NAME@)

target_include_directories(@PROJECT_NAME@::@PROJECT_NAME@
                           INTERFACE "${@PACKAGE_NAME@_INCLUDE_DIR}")
target_link_directories(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE
                        "${@PACKAGE_NAME@_LIB_DIR}")

if(@PACKAGE_NAME@_BUILD_TREE
   AND EXISTS "${CMAKE_CURRENT_LIST_DIR}/@PACKAGE_NAME@-build-config.cmake")
    include("${CMAKE_CURRENT_LIST_DIR}/@PACKAGE_NAME@-build-config.cmake")
else()
    find_package(
        AMDDeviceLibs
        REQUIRED
        CONFIG
        HINTS
        @AMDDeviceLibs_DIR@
        @rocm_version_DIR@
        PATHS
        @AMDDeviceLibs_DIR@
        @rocm_version_DIR@)

    find_package(
        amd_comgr
        @amd_comgr_VERSION@
        EXACT
        REQUIRED
        CONFIG
        HINTS
        @amd_comgr_DIR@
        @rocm_version_DIR@
        PATHS
        @amd_comgr_DIR@
        @rocm_version_DIR@)

    find_package(
        hsa-runtime64
        @hsa-runtime64_VERSION@
        EXACT
        REQUIRED
        CONFIG
        HINTS
        @hsa-runtime64_DIR@
        @rocm_version_DIR@
        PATHS
        @hsa-runtime64_DIR@
        @rocm_version_DIR@)

    find_package(
        hip
        @hip_VERSION@
        EXACT
        REQUIRED
        CONFIG
        HINTS
        @hip_DIR@
        @rocm_version_DIR@
        PATHS
        @hip_DIR@
        @rocm_version_DIR@)

    include("${@PACKAGE_NAME@_CMAKE_DIR}/@PACKAGE_NAME@-config-nolink-target.cmake")

    add_library(@PACKAGE_NAME@::@PACKAGE_NAME@-external-nolink INTERFACE IMPORTED)

    include("${@PACKAGE_NAME@_CMAKE_DIR}/@PACKAGE_NAME@-targets.cmake")

    @PROJECT_NAME@_config_nolink_target(@PACKAGE_NAME@::@PACKAGE_NAME@-external-nolink hip::host)
    @PROJECT_NAME@_config_nolink_target(@PACKAGE_NAME@::@PACKAGE_NAME@-external-nolink hsa-runtime64::hsa-runtime64)
    @PROJECT_NAME@_config_nolink_target(@PACKAGE_NAME@::@PACKAGE_NAME@-external-nolink amd_comgr)

    # Library dependencies
    foreach(TARG @PROJECT_BUILD_TARGETS@)
        set(TARG @PROJECT_NAME@::@PROJECT_NAME@-${TARG})
        if(NOT @PACKAGE_NAME@_FIND_COMPONENTS)
            list(APPEND @PACKAGE_NAME@_LIBRARIES ${TARG})
            target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE ${TARG})
        endif()
    endforeach()

    if(@PACKAGE_NAME@_FIND_COMPONENTS)
        foreach(COMP ${@PACKAGE_NAME@_FIND_COMPONENTS})
            set(TARG @PROJECT_NAME@::${COMP})
            if(TARGET ${TARG})
                set(@PACKAGE_NAME@_${COMP}_FOUND 1)
                list(APPEND @PACKAGE_NAME@_LIBRARIES ${TARG})
                target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@ INTERFACE ${TARG})
            else()
                set(@PACKAGE_NAME@_${COMP}_FOUND 0)
            endif()
        endforeach()
    endif()

    target_link_libraries(@PROJECT_NAME@::@PROJECT_NAME@
                          INTERFACE @PACKAGE_NAME@::@PACKAGE_NAME@-external-nolink)
endif()

add_executable(@PACKAGE_NAME@::rocprofv3 IMPORTED)
set_property(
    TARGET @PACKAGE_NAME@::rocprofv3
    PROPERTY IMPORTED_LOCATION ${@PACKAGE_NAME@_ROOT_DIR}/@CMAKE_INSTALL_BINDIR@/rocprofv3)

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    @PACKAGE_NAME@
    FOUND_VAR @PACKAGE_NAME@_FOUND
    VERSION_VAR @PACKAGE_NAME@_VERSION
    REQUIRED_VARS @PACKAGE_NAME@_ROOT_DIR @PACKAGE_NAME@_INCLUDE_DIR
                  @PACKAGE_NAME@_LIB_DIR @PACKAGE_NAME@_LIBRARIES @PACKAGE_NAME@_VERSION
    HANDLE_COMPONENTS HANDLE_VERSION_RANGE)
